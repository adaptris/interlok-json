import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.*

ext {
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  componentName='Interlok Transform/JSON'
  componentDesc="Everything JSON related; transformations, schemas, json-path (xpath-alike), splitting"
  jacksonVersion = "2.15.2"
  jsonOrgVersion = "20230618"
}


dependencies {
  implementation ("net.sf.json-lib:json-lib:2.4:jdk15") {
    exclude group: 'xerces', module: 'xercesImpl'
  }
  implementation ("xom:xom:1.3.9") {
    exclude group: 'xalan', module: 'xalan'
    exclude group: 'xerces', module: 'xercesImpl'
  }
  api ("org.json:json:$jsonOrgVersion")
  implementation ("net.minidev:json-smart:2.5.0")
  api ("com.jayway.jsonpath:json-path:2.8.0")
  api ("com.bazaarvoice.jolt:jolt-core:0.1.8")
  api ("com.bazaarvoice.jolt:json-utils:0.1.8") {
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
  }
  // Dependency to platform/BOM
  api (platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion"))
  api ("com.fasterxml.jackson.core:jackson-core")
  api ("com.fasterxml.jackson.core:jackson-annotations")
  api ("com.fasterxml.jackson.core:jackson-databind")
  api ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml")
  api ("commons-collections:commons-collections:3.2.2")
  api ("commons-beanutils:commons-beanutils:1.9.4")
  implementation ("xerces:xercesImpl:2.12.2")

  api ("com.github.erosb:everit-json-schema:1.14.2")
  implementation ("com.google.guava:guava:32.1.2-jre")
  api ("com.flipkart.zjsonpatch:zjsonpatch:0.4.14")
  testImplementation ("org.skyscreamer:jsonassert:1.5.1")
  testImplementation ("org.apache.derby:derby:10.15.2.0")
  testImplementation ("org.apache.derby:derbytools:10.15.2.0")
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
               "Build-Jdk": System.getProperty('java.version'),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName)
  }
}


task deleteGeneratedFiles(type: Delete) {
  delete file(testResourcesDir() + "/unit-tests.properties"), file(testResourcesDir() + "/unit-tests.properties.resolved"), 'derby.log', delombokTargetDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
  classifier = 'examples'
  from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

delombok {
  target = delombokTargetDir
}

artifacts {
  archives javadocJar
  archives examplesJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
        asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/pages/cookbook/cookbook-json-transform")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.8.0+")
        properties.appendNode("tags", "json,transform,jdbc")
        properties.appendNode("license", "false")
        properties.appendNode("externalUrl", "http://www.json.org/")
        properties.appendNode("readme", "https://raw.githubusercontent.com/adaptris/interlok-json/develop/interlok-json/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-json")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

javadoc.dependsOn offlinePackageList,umlJavadoc
processTestResources.dependsOn copyUnitTestProperties
clean.dependsOn deleteGeneratedFiles
